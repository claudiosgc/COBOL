       IDENTIFICATION DIVISION.
       PROGRAM-ID.
           BRADMA17.
      *  *****************************************
      *  *    LER E EXIBIR TODOS OS FUNCIONARIOS *
      *  *****************************************
       AUTHOR.
           CLADUIO.

       ENVIRONMENT    DIVISION.
       CONFIGURATION  SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT  IS COMMA.

       INPUT-OUTPUT   SECTION.
       FILE-CONTROL.

       DATA           DIVISION.
       FILE SECTION.

       WORKING-STORAGE SECTION.
       77  WRK-SALARIO-EDIT           PIC ZZZ.ZZ9,99 VALUE ZEROS.
       77  WRK-SQLCODE-EDIT           PIC -999       VALUE ZEROS.

           EXEC SQL
               INCLUDE REGFUN
           END-EXEC.
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
           EXEC SQL
               DECLARE FUNCTEMP CURSOR FOR
                   SELECT *
                   FROM BRADMA.FUNCIONARIOS
                   ORDER BY NOMEFUN
           END-EXEC.

       PROCEDURE      DIVISION.
       0000-PRINCIPAL SECTION.
       0001-PRINCIPAL.
           PERFORM 1001-INICIALIZAR.
           PERFORM 2001-PROCESSAR    UNTIL SQLCODE = 100.
           PERFORM 3001-FINALIZAR.
           STOP RUN.

       1000-INICIALIZAR SECTION.
       1001-INICIALIZAR.
           EXEC SQL
               OPEN FUNCTEMP
           END-EXEC.
           EVALUATE SQLCODE
               WHEN 0
                  PERFORM 4001-LER-FUNCIONARIO
               WHEN 100
                  DISPLAY 'NAO EXISTEM FUNCIONARIOS'
               WHEN OTHER
                   MOVE SQLCODE    TO WRK-SQLCODE-EDIT
                   DISPLAY 'ERRO '    WRK-SQLCODE-EDIT
                           ' NO OPEN DO FUNCTEMP'
      *        FORCAR MAXXCC = 08
                   MOVE 08 TO RETURN-CODE
                   STOP RUN
           END-EVALUATE.

       2000-PROCESSAR SECTION.
       2001-PROCESSAR.
           MOVE DB2-SALFUN            TO WRK-SALARIO-EDIT.

           DISPLAY 'CODIGO...: ' DB2-CODFUN.
           DISPLAY 'NOME.....: ' DB2-NOMEFUN-TEXT.
           DISPLAY 'DEPTO....: ' DB2-DEPTOFUN.
           DISPLAY 'SALARIO..: ' WRK-SALARIO-EDIT.
           DISPLAY 'ADMISSAO.: ' DB2-ADMISSFUN.
           DISPLAY 'FILHOS...: ' DB2-FILHOSFUN.
           DISPLAY 'EMAIL....: ' DB2-EMAILFUN-TEXT.
           DISPLAY ' '.
           PERFORM 4001-LER-FUNCIONARIO.

       3000-FINALIZAR SECTION.
       3001-FINALIZAR.
           EXEC SQL
               CLOSE FUNCTEMP
           END-EXEC.

       4000-LEITURAS SECTION.
       4001-LER-FUNCIONARIO.
           MOVE SPACES    TO DB2-NOMEFUN-TEXT DB2-EMAILFUN-TEXT.
           EXEC SQL
               FETCH FUNCTEMP
               INTO :REG-FUNCIONARIOS
           END-EXEC.
           EVALUATE SQLCODE
               WHEN 0
                   CONTINUE
               WHEN 100
                   DISPLAY 'FIM DA TABELA'
               WHEN OTHER
                   MOVE SQLCODE    TO WRK-SQLCODE-EDIT
                   DISPLAY 'ERRO '    WRK-SQLCODE-EDIT
                           ' NO FETCH DO FUNCTEMP'
      *        FORCAR MAXXCC = 08
                   MOVE 08 TO RETURN-CODE
                   STOP RUN
           END-EVALUATE.
